# ===============================================================
# rnl.peg
#
# Project: Revolution Now
#
# Created by dsicilia on 2020-11-03.
#
# Description: PEG syntax for the RNL language.
#
# ===============================================================
RNL            ← IMPORTS
                 INCLUDES
                 ITEMS

STMT_END       ← ';'

# ===============================================================
# Macros
# ===============================================================
LIST( I, D )   ← (I (~D I)*)?
LIST1( I, D )  ← I (~D I)*

# ===============================================================
# Whitespace / Comments
# ===============================================================
LINE_COMMENT   ←  '#' (!LINE_END .)* LINE_END
LINE_END       ←  '\r\n' / '\r' / '\n' / !.
%whitespace    ←  ([ \t\r\n]+ / LINE_COMMENT)*

# ===============================================================
# Identifiers
# ===============================================================
IDENT          ← <[a-zA-Z_][a-zA-Z0-9_]*>
CPP_TYPE_NAME  ← <'::'? NS_COMPONENT ('::' NS_COMPONENT)* '*'*>
NS_COMPONENT   ← [a-zA-Z_][a-zA-Z0-9_<>]*

# ===============================================================
# Headers
# ===============================================================
IMPORTS        ← IMPORT*
IMPORT         ← 'import' MODULE_NAME ~STMT_END
MODULE_NAME    ← <[a-z][-a-z0-9_]*>

# The first double quotes are in brackets because if we put them
# in single quotes then they'd cause whitespace to be eaten after
# them; we should include all chars inside the double quotes.
INCLUDES       ← INCLUDE*
INCLUDE        ← 'include' INCL_NAME ~STMT_END
INCL_NAME      ← [-a-z0-9_<>"/. ]*

# ===============================================================
# RNL Items
# ===============================================================
ITEMS          ← ITEM*
ITEM           ← NAMESPACE CONSTRUCTS
CONSTRUCTS     ← CONSTRUCT*

CONSTRUCT      ← SUMTYPE # / add_more_here

# ===============================================================
# Namespaces
# ===============================================================
NAMESPACE      ← 'namespace' NS_NAME ~STMT_END
NS_NAME        ← <[a-zA-Z_][a-zA-Z0-9_.]*>

# ===============================================================
# Sum Types
# ===============================================================
SUMTYPE        ← 'sumtype' IDENT '{'
                    TEMPLATES?
                    FEATURES?
                    ALTERNATIVES
                 '}' ~STMT_END

FEATURES       ← '.features' ':' LIST( FEATURE, ',' ) ~STMT_END
FEATURE        ← 'formattable' / 'serializable'
TEMPLATES      ← '.template' ':' LIST1( TMPL_PARAM, ',' ) ~STMT_END
TMPL_PARAM     ← <[A-Z][a-zA-Z0-9_]*>

ALTERNATIVES   ← ALTERNATIVE*
ALTERNATIVE    ← IDENT ':' ALT_VARS
ALT_VARS       ← ALT_VAR*
ALT_VAR        ← CPP_TYPE_NAME IDENT ~STMT_END